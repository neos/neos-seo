prototype(Neos.Seo:AlternateLanguageLinks) < prototype(Neos.Fusion:Component) {
    @class = 'Neos\\Seo\\Fusion\\AlternativeLanguageLinksImplementation'
    @if.languageDimensionExists = ${this.doesLanguageDimensionExist}
    @if.onlyRenderWhenInLiveWorkspace = ${this.node.context.workspace.name == 'live'}
    @if.hasNoForeignCanonical = ${String.isBlank(q(this.node).property('canonicalLink'))}
    @if.hideIfNoIndexIsSet = ${q(this.node).property('metaRobotsNoindex') || q(this.node).is('[instanceof Neos.Seo:NoindexMixin]') ? false : true}

    node = ${documentNode}
    documentVariants = ${this.languageVariantsForRelAlternate}
    dimension = 'language'
    excludedPresets = ${[]}

    # The hreflang value needs to have a format like 'en-US', therefore internally used values
    # like 'en_US' will be modified to match.
    dimensionValueSeparator = '_'

    renderer = afx`
        <Neos.Fusion:Loop items={props.documentVariants} itemName="variant">
            <Neos.Seo:AlternateLanguageLink node={variant.node} hreflang={String.replace(variant.language, props.dimensionValueSeparator, '-')}/>
        </Neos.Fusion:Loop>
    `
}
