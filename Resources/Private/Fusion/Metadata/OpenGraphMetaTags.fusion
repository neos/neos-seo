prototype(Neos.Seo:OpenGraphMetaTags) < prototype(Neos.Fusion:Component) {
    type = ${q(node).property('openGraphType') || 'website'}
    title = ${q(node).property('openGraphTitle') || q(node).property('titleOverride') || q(node).property('title')}
    description = ${q(node).property('openGraphDescription') || q(node).property('metaDescription')}
    image = ${q(node).property('openGraphImage')}
    siteName = ${q(site).property('titleOverride') || site.label}
    locale = Neos.Seo:LangAttribute
    url = Neos.Neos:NodeUri {
        node = ${node}
        absolute = true
    }
    image = Neos.Fusion:Case {
        default {
            @position = 'start'
            condition = ${Type.instance(q(node).property('openGraphImage'), 'Neos\Media\Domain\Model\ImageInterface')}
            renderer = ${q(node).property('openGraphImage')}
        }
    }

    renderer = afx`
        <meta @key="type" property="og:type" content={props.type}/>
        <meta @key="title" property="og:title" content={props.title}/>
        <meta @key="siteName" property="og:site_name" content={props.title}/>
        <meta @key="locale" property="og:locale" content={props.locale} @if.set={props.locale}/>
        <meta @key="description" property="og:description" content={props.description} @if.set={props.description}/>
        <meta @key="url" property="og:url" content={props.url}/>
        <Neos.Fusion:Join @key="image" @if.set={props.image}>
            <meta @key="image" property="og:image" @children="attributes.content">
                <Neos.Neos:ImageUri asset={props.image} preset='Neos.Seo:OpenGraph'/>
            </meta>
            <meta @key="width" property="og:image:width" content={props.image.width}/>
            <meta @key="height" property="og:image:height" content={props.image.height}/>
            <meta @key="alt" property="og:image:alt" content={props.image.label} @if.set={props.image.label}/>
        </Neos.Fusion:Join>
    `
}
