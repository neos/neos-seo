prototype(Neos.Seo:TwitterCard) < prototype(Neos.Fusion:Component) {
    type = ${q(node).property('twitterCardType') || 'summary'}
    title = ${q(node).property('twitterCardTitle') || q(node).property('openGraphTitle') || q(node).property('titleOverride') || q(node).property('title')}
    description = ${q(node).property('twitterCardDescription') || q(node).property('openGraphDescription') || q(node).property('metaDescription')}
    siteHandle = ${Configuration.setting('Neos.Seo.twitterCard.siteHandle')}
    creator = ${q(node).property('twitterCardCreator') || this.siteHandle}
    @context.type = ${this.type}
    url = Neos.Neos:NodeUri {
        node = ${node}
        absolute = true
    }
    image = Neos.Neos:ImageUri {
        asset = Neos.Fusion:Case {
            default {
                @position = 'start'
                condition = ${Type.instance(q(node).property('twitterCardImage'), 'Neos\Media\Domain\Model\ImageInterface')}
                renderer = ${q(node).property('twitterCardImage')}
            }

            opengraph {
                @position = 'after default'
                condition = ${Type.instance(q(node).property('openGraphImage'), 'Neos\Media\Domain\Model\ImageInterface')}
                renderer = ${q(node).property('openGraphImage')}
            }
        }
        preset = Neos.Fusion:Case {
            summaryCardLargeImage {
                condition = ${type == 'summary_large_image'}
                renderer = 'Neos.Seo:TwitterCard.SummaryCardLargeImage'
            }

            default {
                condition = true
                renderer = 'Neos.Seo:TwitterCard.SummaryCard'
            }
        }
        @if.isImageSet = ${this.asset}
    }

    renderer = afx`
        <meta @key="type" name="twitter:card" content={props.type}/>
        <meta @key="title" name="twitter:title" content={props.title}/>
        <meta @key="siteHandle" name="twitter:site" content={props.siteHandle} @if.set={props.siteHandle}/>
        <meta @key="description" name="twitter:description" content={props.description} @if.set={props.description}/>
        <meta @key="creator" name="twitter:creator" content={props.creator} @if.set={props.creator}/>
        <meta @key="url" name="twitter:url" content={props.url}/>
        <meta @key="image" name="twitter:image" content={props.image} @if.set={props.image}/>
    `
}
