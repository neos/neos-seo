prototype(Neos.Seo:XmlSitemap) < prototype(Neos.Fusion:Http.Message) {
    doctype = '<?xml version="1.0" encoding="UTF-8"?>'
    httpResponseHead.headers.Content-Type = 'text/xml'

    body = Neos.Fusion:Component {
        includeAlternateLanguageLinks = false

        items = Neos.Seo:Helper.SitemapUrls {
            startingPoint = ${site}
            includeImageUrls = false
        }

        renderer = afx`
            <urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9"
                    xmlns:image="http://www.google.com/schemas/sitemap-image/1.1"
                    xmlns:xhtml="http://www.w3.org/1999/xhtml/">
                <Neos.Seo:XmlSitemap.UrlList items={props.items} @if.hasItems={props.items}
                    includeAlternateLanguageLinks={props.includeAlternateLanguageLinks}/>
            </urlset>
        `

        @cache.entryTags {
            1 = ${Neos.Caching.nodeTag(this.startingPoint)}
            2 = ${Neos.Caching.descendantOfTag(this.startingPoint)}
        }
    }
}

root.xmlSitemap {
    condition = ${request.format == 'xml.sitemap'}
    type = 'Neos.Seo:XmlSitemap'
}
