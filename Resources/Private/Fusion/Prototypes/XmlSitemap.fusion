prototype(Neos.Seo:XmlSitemap) < prototype(Neos.Fusion:Http.Message) {
    doctype = '<?xml version="1.0" encoding="UTF-8"?>'
    httpResponseHead.headers.Content-Type = 'text/xml'

    body = Neos.Fusion:Template {
        @class = 'Neos\\Seo\\Fusion\\XmlSitemapImplementation'
        templatePath = 'resource://Neos.Seo/Private/Fusion/Prototypes/XmlSiteMap.xml'
        # "root" deprecated since 2.1, removed in 3.0
        root = ${site}
        startingPoint = ${site}
        includeImageUrls = false
        includeAlternateLanguageLinks = false
        renderHiddenInIndex = true

        @context {
            items = ${this.items}
            includeAlternateLanguageLinks = ${this.includeAlternateLanguageLinks}
        }

        urlset = afx`
            <urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9"
                    xmlns:image="http://www.google.com/schemas/sitemap-image/1.1"
                    xmlns:xhtml="http://www.w3.org/1999/xhtml">
                <Neos.Seo:XmlSitemap.UrlList items={items} includeAlternateLanguageLinks={includeAlternateLanguageLinks} @if.hasItems={items}/>
            </urlset>
        `

        @cache.entryTags {
            1 = ${Neos.Caching.nodeTag(this.startingPoint)}
            2 = ${Neos.Caching.descendantOfTag(this.startingPoint)}
        }
    }
}

root.xmlSitemap {
    condition = ${request.format == 'xml.sitemap'}
    type = 'Neos.Seo:XmlSitemap'
}
